plugins {
    // id 'java'
    // id 'application'
    id 'jacoco'
    id 'pmd'
    id 'checkstyle'
    id 'java-library'
    id 'maven-publish'
}

// mainClassName = 'com.weldbit.algo.Algo'
version = '0.0.1'
group = 'com.weldbit'

repositories {
    mavenLocal()
    mavenCentral()
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

configurations {
    checkstyleConfig
}

checkstyle {
    toolVersion '8.39'
}

dependencies {
    testImplementation("org.junit.jupiter:junit-jupiter-engine:5.7.0")
    testImplementation("org.junit.jupiter:junit-jupiter-params:5.7.0")
    testImplementation("org.junit.jupiter:junit-jupiter-api:5.7.0")
    testImplementation("org.junit.platform:junit-platform-runner:1.5.2")
    checkstyleConfig("com.puppycrawl.tools:checkstyle:8.39") { transitive = false }

}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed", "standardOut", "standardError"
    }
    finalizedBy jacocoTestReport // report is always generated after tests run
}

jacocoTestReport {
    dependsOn test // tests are required to run before generating the report
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                minimum = 0.79
            }
        }
    }
}
// to run coverage verification during the build (and fail when appropriate)
check.dependsOn jacocoTestCoverageVerification
project.checkstyle.maxWarnings = 0
project.checkstyle.maxErrors = 0

publishing {
    publications {
        algo(MavenPublication) {
            components.java
            artifact file("$buildDir/libs/algo-${version}.jar")
        }
    }
    repositories {
        maven {
            name = "myRepo"
            url = uri("$projectDir/../../.m2/repository")
        }
    }
}